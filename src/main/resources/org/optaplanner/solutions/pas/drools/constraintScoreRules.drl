package org.optaplanner.solutions.pas.drools;
    dialect "java"

import java.math.BigDecimal;

import org.optaplanner.core.api.score.buildin.hardsoft.HardSoftScoreHolder;
import org.optaplanner.solutions.pas.domain.*;
import org.optaplanner.solutions.pas.shared.*;

global HardSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

rule "quantidadeAlocadaNaoPodeUlrapassarORecursoDisponivelNaFonte"
    when
        $source : FinancialSource($availableResource : availableResource)
        accumulate(
            ResourceAllocation(
                source != null,
                item != null,
                allocationPercentage != null,
                source == $source,
                $calculatedItem : calculationSourceUsed
            );            
            $total : sum($calculatedItem);
            $total > $availableResource
        )
    then
        int $score = ScoreCalculator.calculate($availableResource, $total);
        scoreHolder.addHardConstraintMatch(kcontext, $score);
end

rule "totalDeAlocacaoPorMetaNaoDeveSerMaiorQueCemPorcento"
    when
        $item : PlanningItem($estimatedResource : estimatedResource)
        accumulate(
            ResourceAllocation(
                source != null,
                item != null,
                allocationPercentage != null,
                item == $item,
                $itemPercentage : allocationPercentage.doubleValue
            );
            $total : sum($itemPercentage);
            $total > 100
        )
    then
        int $score = ScoreCalculator.calculate(100, $total);
        scoreHolder.addHardConstraintMatch(kcontext, $score);
end

// ############################################################################
// Soft constraints
// ############################################################################


rule "totalDeAlocacaoPorMetaNaoDeveSerMenorQueCemPorcento"
    when
        $item : PlanningItem($estimatedResource : estimatedResource)
        accumulate(
            ResourceAllocation(
                source != null,
                item != null,
                allocationPercentage != null,
                item == $item,
                $itemPercentage : allocationPercentage.doubleValue
            );
            $total : sum($itemPercentage);
            $total < 100
        )
    then
        int $score = ScoreCalculator.calculate($total, 100);
        scoreHolder.addSoftConstraintMatch(kcontext, $score);
end